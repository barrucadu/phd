Execution of an entire computation proceeds in a stepwise manner: a
thread is chosen by the scheduler, its primitive action is executed,
and a new action is returned to be executed by that thread in the next
step. The simplest thing that a thread can do is to stop, which will
serve as a useful minimal example:

\begin{haskellcode}
stepStop = simple (kill tid threads) Stop
\end{haskellcode}

The effect of \verb|stepStop| is to: remove the current thread from
the map of live threads, and then return the new thread map and the
name of the action to appear in the trace (\verb|Stop|, here). The
\verb|simple| helper function is for actions which don't create any
new shared variables or threads, or have any relaxed-memory effects.

Another simple action that a thread can perform is \verb|AReturn|:

\begin{haskellcode}
stepReturn c = simple (goto c tid threads) Return
\end{haskellcode}

The effect of \verb|stepReturn| is to: extract the continuation of the
action and replace the continuation of the current thread with it. The
\verb|Right|\footnote{The type \texttt{Either a b} type is commonly
  used to represent computations that might fail with an error
  value. By convention \texttt{Left err} means that the computation
  failed with reason \texttt{err}, and \texttt{Right x} means that the
  computation succeeded, producing \texttt{x}.}  indicates that the
action completed successfully. There are a few different possible
failures, such as an uncaught exception, which will terminate the
current thread. If the main thread is terminated, the entire
computation terminates.

\subsubsection{Threading}
\label{sec:execution-stepwise-threading}

\begin{haskellcode}
type Threads n r s = Map ThreadId (Thread n r s)
\end{haskellcode}

A map is used to keep track of all current threads. There are helper
functions to manipulate this map: \verb|kill| and \verb|goto| are two;
another is \verb|launch|, used to create a new thread. This can be
seen in the implementation of \verb|AFork|:

\begin{haskellcode}
stepFork name a b = return (Right (threads', idSource', Fork newtid, wb)) where
  threads' = goto (b newtid) tid (launch tid newtid a threads)
  (idSource', newtid) = nextTId name idSource
\end{haskellcode}

The \verb|stepFork| function involves two modifications to the thread
map: firstly, a new thread is created (and inherits the masking state
of its parent); secondly the continuation of the current thread is
updated. Here \verb|simple| cannot be used, as the identifier source
is being modified.

In the implementation of \verb|AYield|, no special functionality is
needed:

\begin{haskellcode}
stepYield c = simple (goto c tid threads) Yield
\end{haskellcode}

Its effect is purely a scheduling concern; from the point of view of
updating the state of the system, it is no different to
\verb|AReturn|.

\subsubsection{\texttt{CRef}s and Relaxed Memory}
\label{sec:execution-stepwise-cref}

\begin{haskellcode}
newtype CRef r a = CRef (CRefId, r (Map ThreadId a, Integer, a))
\end{haskellcode}

A \verb|CRef| is implemented as a mutable reference containing a
\emph{globally visible} value, a counter of how many write commits
there have been, and a number of \emph{thread-specific} values. These
thread-specific values correspond to uncommitted writes. A
\verb|Ticket|, used in compare-and-swap operations, is a
\emph{witness} that a specific prior value was observed. Like threads,
a \verb|CRef| (and \verb|CVar|) can be given a name when it is
initially created.

There are three memory models supported by \dejafu{}. Each has a
different implementation for writing to a \verb|CRef|:

\begin{haskellcode}
stepWriteRef cref@(CRef (crid, _)) a c = case memtype of
\end{haskellcode}

The first model assumes sequential consistency. There are no relaxed
memory effects:\footnote{This is an emple of \emph{do notation}, which
  is a convenient synctatic sugar for composition of monadic actions.}

\begin{haskellcode}
  SequentialConsistency -> do
    writeImmediate cref a
    simple (goto c tid threads) (WriteRef crid)
\end{haskellcode}

The \verb|writeImmediate| function writes to the globally visible
value, and clears the thread-specific values.

Total store order (TSO) corresponds to an architecture where each
thread has its own cache. This matches modern x86 and x86\_64
processors. Writes made by a thread will be cached, but they will be
committed in that same order to main memory:

\begin{haskellcode}
  TotalStoreOrder -> do
    wb' <- bufferWrite wb tid cref a tid
    return (Right (goto c tid threads, idSource, WriteRef crid, wb'))
\end{haskellcode}

The \verb|bufferWrite| function appends a write to the relevant write
buffer, in this case the one corresponding to that thread.

Partial store order (PSO) is a more relaxed version of total store order,
where the writes a thread makes may not necessarily be committed in
order. It can be modelled by giving each \verb|CRef| a write buffer,
rather than each thread:

\begin{haskellcode}
  PartialStoreOrder -> do
    wb' <- bufferWrite wb crid cref a tid
    return (Right (goto c tid threads, idSource, WriteRef crid, wb'))
\end{haskellcode}

Both the TSO and PSO cases update the thread-specific map. A thread
will always see the writes it has made, but other threads may not.

The compare-and-swap write is a little different. It has the effect of
a memory barrier: any uncommitted writes to any \verb|CRef| are
committed before the CAS is done, and the result is immediately
globally visible. There is a \verb|synchronised| function for actions
with this barrier property:\footnote{The \texttt{\$} operator is
  function application, but with a very low precedence. This makes it
  convenient for avoiding parentheses, which can be more readable when
  multi-line expressions are involved.}

\begin{haskellcode}
stepCasRef cref@(CRef (crid, _)) tick a c = synchronised $ do
  (suc, tick') <- casCRef cref tid tick a
  simple (goto (c (suc, tick')) tid threads) (CasRef crid suc)
\end{haskellcode}
%$

The \verb|casCRef| function here generates a new \verb|Ticket|,
compares with the supplied one, and then swaps the value. It is
provided, rather than the logic be included verbatim, as it is used
again in the implementation of \verb|stepModRefCas|.

The implementation of \verb|synchronised| is as follows:

\begin{haskellcode}
synchronised ma = do
  writeBarrier wb
  res <- ma
  case res of
    Right (threads', idSource', act', _) -> return
      (Right (threads', idSource', act', emptyBuffer))
    _ -> return res
\end{haskellcode}

Here \verb|writeBarrier| commits all cached writes. The action is then
executed, and an empty write buffer returned. So \verb|simple| can be
used in the implementation of \verb|stepModRef| despite the write
buffer being changed.

Cached writes can be committed to the globally visible value (at which
point the thread-specific values disappear) by executing an
\verb|ACommit| action:

\begin{haskellcode}
stepCommit t c = do
  wb' <- case memtype of
    TotalStoreOrder   -> commitWrite wb t
    PartialStoreOrder -> commitWrite wb c
  return (Right (threads, idSource, CommitRef t c, wb'))
\end{haskellcode}

Note how the invocation of \verb|commitWrite| differs between the
cases: under TSO, the cache corresponding to the thread is used;
whereas under PSO, the cache corresponding to the \verb|CRef| is
used. There is no case for sequential consistency here, as commit
actions are not explicitly introduced by the program under test; they
are introduced by the test runner when executing under a relaxed
memory model. See \sect{execution}{scheduling}.

Reading a reference is quite simple:

\begin{haskellcode}
stepReadRef cref@(CRef (crid, _)) c = do
  val <- readCRef cref tid
  simple (goto (c val) tid threads) (ReadRef crid)

stepReadRefCas cref@(CRef (crid, _)) c = do
  tick <- readForTicket cref tid
  simple (goto (c tick) tid threads) (ReadRefCas crid)
\end{haskellcode}

The \verb|readCRef| function checks if there is a cached value for the
current thread and, if so, returns it. Otherwise it returns the
globally visible value. The \verb|readForTicket| function behaves
similarly, but returns a \verb|Ticket| rather than the current value.

\subsubsection{Exceptions}
\label{sec:execution-stepwise-exception}

A thread has both a stack of exception handlers, and a masking
state. The handler stack affects all exceptions raised in the thread,
whereas the masking state only affects exceptions raised by
\verb|AThrowTo|.

\begin{haskellcode}
stepCatching h ma c = simple threads' Catching where
  a     = runCont ma      (APopCatching . c)
  e exc = runCont (h exc) (APopCatching . c)
  threads' = goto a tid (catching e tid threads)
\end{haskellcode}

Note the addition of \verb|APopCatching| at the ends of the enclosed
action and the handler. This ensures that the handler is popped from
the stack whether an exception is thrown or not.

When an exception is thrown, it may not be able to be handled by the
topmost handler, as there are exceptions of many types:

\begin{haskellcode}
stepThrow e = case propagate e tid threads of
    Just threads' -> simple threads' Throw
    Nothing -> return (Left UncaughtException)
\end{haskellcode}

The \verb|propagate| function pops from the stack of exception
handlers until one is found capable of handling that type of
exception. It then jumps to the handler, and returns the new thread
map. If no handler was found, the thread is killed by the uncaught
exception.

Throwing an exception to another thread is significantly more
complicated, and is also a \verb|synchronised| operation:

\begin{haskellcode}
stepThrowTo t e c = synchronised $
  let threads' = goto c tid threads
      blocked  = block (OnMask t) tid threads
  in if interruptible (lookup t threads)
     then case propagate e t threads' of
            Just threads'' -> simple threads'' (ThrowTo t)
            Nothing
              | t == 0    -> return (Left UncaughtException)
              | otherwise -> simple (kill t threads') (ThrowTo t)
     else simple blocked (BlockedThrowTo t)
\end{haskellcode}
%$

Firstly, whether the thread is interruptible is checked. If not, the
current thread is blocked. If it is interruptible, then the exception
is propagated through its handler stack. If a handler is found, the
thread jumps to it, throwing away whatever it was going to do next. If
a handler is not found, the thread is killed. If the main thread is
killed, the entire computation terminates.

Like \verb|ACatching|, the \verb|AMasking| action introduces an
additional action into its continuation:

\begin{haskellcode}
stepMasking m ma c = simple threads' (SetMasking False m) where
  a = runCont (ma umask) (AResetMask False False m' . c)
  m' = masking (lookup tid threads)
  umask mb = do
    resetMask True m'
    b <- mb
    resetMask False m
    return b
  resetMask typ ms = cont (\k -> AResetMask typ True ms (k ()))
  threads' = goto a tid (mask m tid threads)
\end{haskellcode}

\subsubsection{Software Transactional Memory}
\label{sec:execution-stepwise-stm}

As STM transactions are atomic, the implementation is comparatively
simple. They are still implemented in terms of a step function, but it
is just iterated until termination.

Firstly, the transaction is executed:

\begin{haskellcode}
stepAtom stm c = synchronised $ do
  (res, newctvid) <- runstm stm (nextCTVId idSource)
  let idSource'   = idSource { nextCTVId = newctvid }
  case res of
\end{haskellcode}
%$

There are now three possible results:

\begin{enumerate}
\item The transaction succeeds. All threads blocked on \verb|CTVar|s
  which were modified are woken:

\begin{haskellcode}
    Success readen written val
      let (threads', woken) = wake (OnCTVar written) threads
      in return (Right (goto (c val) tid threads', idSource', STM woken, wb))
\end{haskellcode}

\item The transaction aborts due to \verb|retry|. The thread is
  blocked until any of the read \verb|CTVar|s are modified:

\begin{haskellcode}
    Retry touched ->
      let threads' = block (OnCTVar touched) tid threads
      in return (Right (threads', idSource, BlockedSTM, wb))
\end{haskellcode}

\item The transaction aborts due to an uncaught exception. The
  exception is thrown in the thread:

\begin{haskellcode}
    Exception e -> stepThrow e
\end{haskellcode}

\end{enumerate}
